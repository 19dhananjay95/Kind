type App.GG.Body.Triple{ 
  new(fst: App.GG.Chip, snd: App.GG.Chip, trd: App.GG.Chip)
}

type App.GG.Body {
  new(
    hull : App.GG.Body.Hull
    upper: App.GG.Body.Upper
    lower: App.GG.Body.Lower
    arm_1: App.GG.Body.Arms
    arm_2: App.GG.Body.Arms
  )
}

App.GG.Body.get_correspondent_chip(
  chips: App.GG.Chips
  id: Nat
): App.GG.Chip

  List.get!(id, chips) <> App.GG.Chip.Generic

App.GG.Body.generic(
  body: App.GG.Parts,
  chips: App.GG.Chips
): App.GG.Body.Triple

  let f = App.GG.Body.get_correspondent_chip(chips)
  {f(body@fst), f(body@snd) , f(body@trd)}

//============================================

App.GG.Body.chips(
  body: App.GG.Body
): App.GG.Chips

  let hull  = App.GG.Parts.chips_list(body@hull@parts,  App.GG.Parts.hull(body@hull@form))
  let upper = App.GG.Parts.chips_list(body@upper@parts, App.GG.Body.Upper.chips)
  let lower = App.GG.Parts.chips_list(body@lower@parts, App.GG.Body.Lower.chips)
  let arm_1 = App.GG.Parts.chips_list(body@arm_1@parts, App.GG.Body.Arms.chips)
  let arm_2 = App.GG.Parts.chips_list(body@arm_2@parts, App.GG.Body.Arms.chips)
  let chips = hull ++ upper ++ lower ++ arm_1 ++ arm_2
  chips

App.GG.Body.attributes(
  body: App.GG.Body
): App.GG.Attributes
  let chips = App.GG.Body.chips(body)
  let f = (chip: App.GG.Chip, attributes: App.GG.Attributes) App.GG.Attributes.add(attributes, chip@attributes)
  let attributes = List.foldr!!(App.GG.Attributes.pure, f, chips)
  attributes

App.GG.Body.to_folder(body: App.GG.Body): App.GG.Chips
  let chips = App.GG.Body.chips(body)
  let f = 
    (chip: App.GG.Chip)
      [chip, chip, chip]
  let new_list = List.map!!(f, chips)
  let flat = List.flatten!(new_list)
  flat
