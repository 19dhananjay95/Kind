Fm.Defs.report(defs: Fm.Defs, names: List(Fm.Name)): String
  let types = Fm.Defs.report.types(defs, names)
  let errors = Fm.Defs.report.errors(defs)
  let errors = case errors {
    nil  : "All terms check."
    cons : errors
  }
  types | "\n" | errors
    
Fm.Defs.report.errors(defs: Fm.Defs): String
  let errors = ""
  for key in Map.keys!(defs) with errors:
    let name = Fm.Name.from_bits(key)
    case Fm.get<_>(name, defs) as got {
      none:
        errors
      some:
        open got.value
        case got.value.stat {
          init: errors
          wait: errors
          done: errors
          fail: case got.value.stat.errors {
            nil:
              errors
            cons:
              let name_str = got.value.name
              let rel_errs = Fm.Error.relevant(got.value.stat.errors, Bool.false)
              for err in rel_errs with errors:
                let err_msg = Fm.Error.show(err, defs)
                let ori_msg = case Fm.Error.origin(err) as origin {
                  none:
                    errors
                  some:
                    open origin.value
                    let inside = "Inside '" | got.value.file | "':\n"
                    let source = Fm.highlight(got.value.code, origin.value.fst, origin.value.snd)
                    inside | source
                }
                errors | err_msg | ori_msg | "\n\n"
          }
        }
    }

Fm.Defs.report.types(defs: Fm.Defs, names: List(Fm.Name)): String
  let types = ""
  for name in names with types:
    case Fm.get!(name, defs) as got {
      none:
        types
      some:
        open got.value
        types | name | ": " | Fm.Term.show(got.value.type) | "\n"
    }
