Web.Kaelin.Draw.state.background(map: Web.Kaelin.Map, cast_info: Maybe<Web.Kaelin.CastInfo>, env_info: App.EnvInfo, img:VoxBox): VoxBox
let list = NatMap.to_list!(map)
  for pair in list with img:
    open pair
    let coord = Web.Kaelin.Coord.Convert.nat_to_axial(pair.fst)
    let {i, j} = Web.Kaelin.Draw.support.centralize(coord)
    let hex_effect = case cast_info {
      none: Web.Kaelin.HexEffect.normal
      some: 
        open cast_info.value
        open cast_info.value.skill
        let range = Web.Kaelin.Coord.range(cast_info.value.hero_pos, cast_info.value.skill.range) // creates a list for all tiles inside cast range
        Web.Kaelin.Draw.support.which_effect(coord,cast_info.value,range) // Returns the cast affecting a tile
    }
    let mouse_over = Web.Kaelin.Draw.support.mouse_on_coord(coord,env_info) // Checks if mouse is over a tile
    let sprite = Web.Kaelin.Terrain.Sprite.new(hex_effect, mouse_over) // Function to return the image for the tile
    let tile = Web.Kaelin.Map.get(coord,map) <> []
      for entity in tile with img:
        case entity {
          background: open entity.terrain
            VoxBox.Draw.image(i, j, 0#32, entity.terrain.draw(sprite), img)
          creature: img
        }
    img  
img
