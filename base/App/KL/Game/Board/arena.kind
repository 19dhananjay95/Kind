// The 'Arena' board
App.KL.Game.Board.arena: App.KL.Game.Board
  extra_width  = App.KL.Constants.board_extra_width
    board_size = App.KL.Constants.board_size
  n_board_size = I32.neg(board_size)

  board  = Hexagonal.Axial.BBT.new<App.KL.Game.Tile>
  
  
  new_terrain = App.KL.Game.Terrain.new(0)
  new_terrain = App.KL.Game.Entity.terrain(new_terrain)
  
  neutral_token = App.KL.Game.Token.new(0, 1, 1, 2)
  neutral_token = App.KL.Game.Entity.token(neutral_token)
  token_coords  = [{0, 0} :: Hexagonal.Axial, 
                   {0,-5} :: Hexagonal.Axial, 
                   {0, 5} :: Hexagonal.Axial]
    
  for j : I32 from n_board_size to (board_size + 1) with board:
    // https://www.redblobgames.com/grids/hexagons/#range-coordinate
    // iterates only in valid coordinates
    let a = I32.max(n_board_size, I32.neg(j) - board_size)
    let b = I32.min(  board_size, I32.neg(j) + board_size)

    for i : I32 from (a - extra_width) to (b + extra_width + 1) with board: 
      // generate int axial coordinate
      coord   = Hexagonal.Axial.new(i, j)
      let board = App.KL.Game.Board.push(coord, new_terrain, board)
      
      case List.find!(Hexagonal.Axial.eql(coord), token_coords) as token_coord {
        none: board
        some: App.KL.Game.Board.push(coord, neutral_token, board)
      }

    board
  board

