. true  [t] [f] t
. false [t] [f] f
. not   [b] [t] [f] (b f t)

. zero [s] # [z] z
. succ [n] [s] [s = s] [k = (n #s)] # [z] (s (k z))
. mul  [a] [b] [s] [S = s] [A = [B = (b #S)] (a #B)] # [z] (A z)

. cons [x] [xs] [c] [n] (c x xs)
. nil  [c] [n] n
. head [x] [xs] (xs [x] [xs] x x)
. tail [xs] (xs [x] [xs] xs nil)

. pair [a] [b] [pair] (pair a b)
. fst  [a] [b] a
. snd  [a] [b] b

. n0 [s]         # [z] z
. n1 [s] [s = s] # [z] (s z)
. n2 [s] [s = s] # [z] (s (s z))
. n3 [s] [s = s] # [z] (s (s (s z)))
. n4 (mul n2 n2)
. n8 (mul n4 n2)
. n16 (mul n8 n2)
. n32 (mul n16 n2)
. n64 (mul n32 n2)
. n128 (mul n64 n2)
. n256 (mul n128 n2)
. n65536 (mul n256 n256)

. fib [n]
  let init (pair #0 #1)
  let next [p] [pair] (p [a] [b] [A = a] [B = b] (pair #B #{A + B}))
  [F = (n #next)] # (F init [a] [b] a)

// outputs 832827963 (which is fib(65536) % 2^32), in 393555 graph rewrites
. fib_test
  (fib n65536)

. add_test
  {3 + 2}

. mul_test
  {3 * 2}

. fun_test
  ([x] {x + 7} 1)

. inc_test
  [F = (n2 #[x]{x + 1})]
  # (F 0)

. get_fst_char_test
  [F = ("test" #cons)]
  # {(head 0 (tail (F nil))) >> 24}

. incs_test
  [F = (n256 #[x]{x + 1})]
  # (F 0)

. main
  fib_test
