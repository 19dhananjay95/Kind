App.GG.Effect.status.add_at(
  status: App.GG.Creature.Status
  pos: App.GG.Coord
): App.GG.Effect<Unit>
  let status_add = App.GG.Creature.Status.add(status)
  let mod = App.GG.Creature.modify_at(status_add, pos)
  App.GG.Effect.grid.modify(mod)

App.GG.Effect.status.add(status: App.GG.Creature.Status): App.GG.Effect<Unit>
  App.GG.Effect {
    get center = App.GG.Effect.coord.get_center
    App.GG.Effect.status.add_at(status, center)
  }

App.GG.Effect.status.counter.add(effect: App.GG.Coord -> App.GG.Effect<Unit>, duration: F64): App.GG.Effect<Unit>
  App.GG.Effect { 
    get center = App.GG.Effect.coord.get_center
    App.GG.Effect.status.counter.add_at(effect, duration, center)
  }


App.GG.Effect.status.counter.add_at(effect: App.GG.Coord -> App.GG.Effect<Unit>, duration: F64, coord: App.GG.Coord): App.GG.Effect<Unit>
  let uduration = F64.to_u64(duration * 60)
  let counter = App.GG.Creature.Status.counter.create(effect, uduration)
  App.GG.Effect { 
    get status = App.GG.Effect.status.get_at(2, coord)
    without status: App.GG.Effect.pass
    App.GG.Effect.status.add_at(counter, coord)
  }

App.GG.Effect.status.burn.add_at(dpa: I32, amount: Nat, pos: App.GG.Coord): App.GG.Effect<Unit>
  App.GG.Effect { 
    let create = App.GG.Creature.Status.burn.create(dpa, amount)
    get status = App.GG.Effect.status.get_at(1, pos)
    without status: App.GG.Effect.pass
    without status: App.GG.Effect.status.add_at(create, pos)
    let new_status = App.GG.Creature.Status.burn.add(create, status)
    App.GG.Effect.status.add_at(new_status, pos)
  }

//Ids for status
  //0: barrier
  //1: burn
  //2: counter
  //3: intangible
  //4: intangible_immunity
  //5: pendent_movement
  //6: shield
  //7: stun


//===============================================
App.GG.Effect.status.get_at(id: Nat, pos: App.GG.Coord): App.GG.Effect<Maybe<Maybe<App.GG.Creature.Status>>>
  App.GG.Effect {
    get creature = App.GG.Effect.creature.get_at(pos)
    without creature: App.GG.Effect {return none}
    App.GG.Effect {
      return App.GG.Creature.Status.get_from_nat(id, creature@statuses)
    }
  }

App.GG.Effect.status.has_status(id: Nat, coord: App.GG.Coord): App.GG.Effect<Bool>
  App.GG.Effect { 
    get status = App.GG.Effect.status.get_at(id, coord)
    without status: App.GG.Effect { return Bool.false }
    without status: App.GG.Effect { return Bool.false } 
      App.GG.Effect { 
        return true
      }
  }
