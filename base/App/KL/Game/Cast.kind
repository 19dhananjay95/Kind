type App.KL.Game.Cast {
  new(
    player: String
    target: Hexagonal.Axial
    letter: Char
  )
}

type App.KL.Game.Cast.Preview {
  new(
    areas: Hexagonal.Axial.BBL<App.KL.Game.Indicator>
    picks: Hexagonal.Axial.BBL<Char>
  )
}

// Pushes a cast to a cast list
App.KL.Game.Cast.push(
  player: String
  target: Hexagonal.Axial
  letter: Char
  casts: List<App.KL.Game.Cast>
): List<App.KL.Game.Cast>
  App.KL.Game.Cast.new(player, target, letter) & casts

// Returns the current picks of a player as a map of Coord->Letter
App.KL.Game.Cast.picks_of(
  player: String
  casts: List<App.KL.Game.Cast>
): Hexagonal.Axial.BBL<Char>
  let map = Hexagonal.Axial.BBL.new!
  for cast in casts with map:
    open cast
    if player =? cast.player then 
      Hexagonal.Axial.BBL.insert!(cast.target, cast.letter, map)
    else
      map
  map
  
// Gets the skill associated with a cast
App.KL.Game.Cast.get_skill(
  cast: App.KL.Game.Cast
  game: App.KL.Game
): Maybe<App.KL.Game.Skill>
  open game
  open cast
  Maybe {
    get player = game.players{cast@player}
    get hero_id = player@hero_id
    get hero = App.KL.Game.Hero.get_by_id(hero_id)
    get skill = hero@skills{Char.to_string(cast.letter)}
    return skill
  }

// Gets the coord associated with a cast
App.KL.Game.Cast.get_coord(
  cast: App.KL.Game.Cast
  game: App.KL.Game
): Maybe<Hexagonal.Axial>
  App.KL.Game.Board.find_player_coord(cast@player, game@board)

// Gets the creature associated with a cast
App.KL.Game.Cast.get_creature(
  cast: App.KL.Game.Cast
  game: App.KL.Game
): Maybe<App.KL.Game.Creature>
  Maybe {
    get coord = App.KL.Game.Cast.get_coord(cast, game)
    get tile = Hexagonal.Axial.BBL.get!(coord, game@board)
    tile@creature
  }

// Gets the hero associated with a cast
App.KL.Game.Cast.get_hero(
  cast: App.KL.Game.Cast
  game: App.KL.Game
): Maybe<App.KL.Game.Hero>
  Maybe {
    get creature = App.KL.Game.Cast.get_creature(cast, game)
    return creature@hero
  }

// Sorts a game's casts by delay, in ascending order
App.KL.Game.Cast.sort(game: App.KL.Game): List<App.KL.Game.Cast>
  open game
  let casts = [] :: List<Pair<App.KL.Game.Cast,U64>>
  for cast in game.casts with casts:
    case App.KL.Game.Cast.get_skill(cast, game) as got_skill {
      none: casts
      some: {cast, got_skill.value@delay} & casts
    }
  let list = List.sort!((a,b) U64.cmp(a@snd, b@snd), casts)
  let list = List.map!!((a) a@fst, list)
  list

// Applies a cast, modifying the game state
App.KL.Game.Cast.apply(
  cast: App.KL.Game.Cast
  game: App.KL.Game
): App.KL.Game
  open game
  open cast
  Maybe {
    get skill = App.KL.Game.Cast.get_skill(cast, game)
    get coord = App.KL.Game.Cast.get_coord(cast, game)
    return App.KL.Game.Effect.run!(skill@effect, coord, cast@target, game)
  } <> game
