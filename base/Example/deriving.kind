type Example.deriving.MyType {
  foo(n: List<Nat>, s: String, m: Example.deriving.MyType)
  bar
} deriving (stringifier, parser, serializer, deserializer)

Example.deriving: _
  IO {
    let val = Example.deriving.MyType.foo([1,2,3], "Hello", Example.deriving.MyType.bar)

    // Converts to string
    let str = Stringifier.run!(Example.deriving.MyType.stringifier, val)
    IO.print("str: " | str)

    // Parses string to a value
    let val = Parser.run!(Example.deriving.MyType.parser, str) <> Example.deriving.MyType.bar

    // Serializes to bits
    let bts = Serializer.run!(Example.deriving.MyType.serializer, val)
    IO.print("bts: " | Bits.show(bts))

    // Deserializes to a value
    let val = Deserializer.run!(Example.deriving.MyType.deserializer, bts) <> Example.deriving.MyType.bar

    // Converts to string again
    let str = Stringifier.run!(Example.deriving.MyType.stringifier, val)
    IO.print("str: " | str)
  }
