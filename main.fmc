// ::::::::::
// :: Bool ::
// ::::::::::

def true  [t] [f] t
def false [t] [f] f
def not   [b] [t] [f] (b f t)

// :::::::::
// :: Nat ::
// :::::::::

def zero [s] # [z] z
def succ [n] [s] [s = s] [k = (n #s)] # [z] (s (k z))
def mul  [a] [b] [s] [S = s] [A = [B = (b #S)] (a #B)] # [z] (A z)

// ::::::::::
// :: List ::
// ::::::::::

def cons   [x] [xs] [c] [n] (c x xs)
def nil    [c] [n] n
def head   [x] [xs] (xs [x] [xs] x x)
def tail   [xs] (xs [x] [xs] xs nil)
def concat [a] [b] [cons] [cons = cons] [nil] (a #[h] [t] (cons h t) (b #cons nil))

// :::::::::::
// :: Tuple ::
// :::::::::::

def tup [a] [b] [tup] (tup a b)
def fst [p] (p [a] [b] a)
def snd [p] (p [a] [b] b)

// :::::::::
// :: Num ::
// :::::::::

def fib [n]
  let init (tup #0 #1)
  let next [p] [tup] (p [a] [b] [A = a] [B = b] (tup #B #{A + B}))
  [F = (n #next)] # (F init [a] [b] a)

// ::::::::::::
// :: String ::
// ::::::::::::

def to_chars [str] [cons] [cons = cons]
  [f =
    (snd str #[c] [c = c] [cs]
      let c0 #{{c >>  0} & 0xFF}
      let c1 #{{c >>  8} & 0xFF}
      let c2 #{{c >> 16} & 0xFF}
      let c3 #{{c >> 24} & 0xFF}
      (cons c0 (cons c1 (cons c2 (cons c3 cs)))))]
  # [nil] (f nil)

// ::::::::::
// :: Term ::
// ::::::::::

def Lam [body]        [Lam] [App] (Lam body)
def App [func] [argm] [Lam] [App] (App func argm)

def redex [term]
  let lam [body]
    (Lam body)
  let app [func]
    let app-lam [lam-body] [argm]
      (lam-body argm) 
    let app-app [app-func] [app-argm] [argm]
      (App (App app-func app-argm) argm)
    (func app-lam app-app)
  (term lam app)

def add_test
  {3 + 2}

def mul_test
  {3 * 2}

def incs_test
  [F = (~256 #[x]{x + 1})]
  # (F 0)

def pair_test
  @0 (& 42 1337)

def if_test
  ? 1
  & "cond is true"
  : "cond is false"

// outputs 832827963 (which is fib(65536) % 2^32), in 393481 graph rewrites
def fib_test
  (fib ~65536)

def get_fst_char_test
  [F = ("test" #cons)]
  # {(head 0 (tail (F nil))) >> 24}

def main
  if_test
