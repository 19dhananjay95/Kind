// CARD INFO FOR USER AND ALLIES
// -------

// draw card info for user and allies
App.KL.Game.Draft.cards(players: Map<App.KL.Game.Player>, user: String): DOM
  let team   = App.KL.Game.Draft.to_team(players, user) <> App.KL.Game.Team.neutral
  let player = players{user}
  let allies = Map.delete!(user, players)
  let hero   = case player{
    none: none
    some: player = player.value
      player@hero_id 
  }
  
  <div style={
    "width": "70%"
    "height": "100%"
    "display": "flex"
  }>
    { App.KL.Game.Draft.cards.picks_left(hero)} // draw card info for user
    { App.KL.Game.Draft.cards.picks_right(allies, team)} // draw card info for allies
  </div>


// USER
// -----

// draw card info for user
App.KL.Game.Draft.cards.picks_left(hero: Maybe(U8)): DOM
  <div style={
    "width": "40%"
    "height": "100%"
    "display": "flex"
    "padding": "5%"
    "box-sizing": "border-box"
  }>
    { App.KL.Game.Draft.cards.player(hero)}
  </div>

App.KL.Game.Draft.cards.player(hero: Maybe(U8)): DOM
  let {name, image} = Maybe {
      get hero_id = hero
      get hero = App.Kaelin.Hero.info(hero_id) // TODO bring this function
      let assets = hero@assets
      return {hero@name,assets@base64}
    } <> {"Choosing", App.KL.Game.Draft.cards.interrogation}
  
  App.KL.Game.Draft.cards.card(name, image, "100%")

// ALLIES
// -----

// draw card info for allies
App.KL.Game.Draft.cards.picks_right(map: Map<App.KL.Game.Player>, team: App.KL.Game.Team): DOM
  <div style={ 
    "width": "60%"
    "height": "100%"
    "padding": "3%"
    "display": "flex"
    "flex-direction": "row"
    "align-items": "center"
    "box-sizing": "border-box"
  }>
    for div in App.KL.Game.Draft.cards.allies(map, team): div
  </div>

App.KL.Game.Draft.cards.allies(map: Map<App.KL.Game.Player>, team: App.KL.Game.Team): List<DOM>
  lst       = Map.to_list!(map)
  teammates = []
  for info in lst with teammates:
    open info
    if App.KL.Game.Team.eql(team, info.snd@team) then
      info & teammates
    else
      teammates
  count = 2 - List.length!(teammates)
  
  dom   = []
  for i from 0 to count with dom:
    App.KL.Game.Draft.cards.ally("none", none) & dom
  for pair in teammates with dom:
    App.KL.Game.Draft.cards.ally(pair@fst, some(pair@snd)) & dom

  dom

App.KL.Game.Draft.cards.ally(user: String, info: Maybe(App.KL.Game.Player)): DOM
  {name, image} = case info {
    none: {"Connecting", App.KL.Game.Draft.cards.interrogation}
    some:
      Maybe {
        get info = info.value@hero_id
        get hero = App.Kaelin.Hero.info(info)  // TODO bring this function
        let assets = hero@assets
        return {hero@name, assets@base64}
      } <> {"Choosing", App.KL.Game.Draft.cards.interrogation}
    }

  App.KL.Game.Draft.cards.card(name, image, "80%")

App.KL.Game.Draft.cards.card(name: String, image: String, height: String): DOM
   <div style={ 
    "width": "100%"
    "height": height
    "display": "flex"
    "border": "5px solid #d6dadc"
    "background-color": "#bac1c4"
    "margin": "3%"
    "border-radius": "5px"
    "flex-direction": "column"
    "justify-content": "space-between"
    "align-items": "center"
    "box-sizing": "border-box"
  }>
    <div style={ 
      "height": "10%"
      "margin-top": "5%"
    }>
      name
    </div>
    
    <div style={ 
      "width":"50%" 
      "height":"auto"
    }>
      <img src=image style={ 
        "width": "100%"
        "height": "auto"
        "image-rendering": "pixelated"
      }></img>
    </div>
    
    <div style={ 
      "width": "100%"
      "height": "40%"
      "background-color": "#d6dadc"
    }></div>

  </div>


App.KL.Game.Draft.cards.interrogation: String
  "data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAABQAAAAUCAYAAACNiR0NAAAAn0lEQVQ4T8WUURKAIAhE44x1zO5og2mDKwiONflZ8tx1EdpeXuTwkvHfrBsBM+wsxD3dbKKnRK21gA1MqmTwCGoCq7IDPPN3Ae3qo8C6L30CZNFFeVwhOOXC5l5ngdgtXUgrQAuWuwhP9hqb9+cg6hKpT/WhPPgBejBVsvLUEDh0FbKsJG6OgAhwah5FgDhx1i17z01aCCmEQbGu8NdQLmHYLhXjuqBcAAAAAElFTkSuQmCC"