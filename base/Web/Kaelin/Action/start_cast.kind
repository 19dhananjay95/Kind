Web.Kaelin.Action.start_cast(key_code: U16, state: Web.Kaelin.State): Web.Kaelin.State
  case state {
    game: 
      let player_info = Web.Kaelin.Map.player.info(state.user, state.map)
      case player_info {
        none: state
        some: 
          let {player_coord, player_entity} = player_info.value
          case player_entity {
            background: state
            creature:     
              open player_entity
              let skill = Web.Kaelin.Hero.skill.from_key(key_code, player_entity.hero)
              case skill {
                none: state
                some:           
                  open skill.value as skill_value
                  // calculate range of skill
                  let range = Web.Kaelin.Coord.range(player_coord, skill_value.range)
                  // create cast info
                  let cast = Web.Kaelin.CastInfo.new(
                    player_coord,
                    Web.Kaelin.HexEffect.skill,
                    skill.value,
                    range
                  )
                  Web.Kaelin.State.game.set_cast_info(cast, state)
              }
          } 
      }
  } default state